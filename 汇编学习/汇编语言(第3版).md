# 第8章 数据处理的两个基本问题

1. 处理的数据在什么地方？
2. 要处理的数据有多长？

> 寄存器的分类
>
> 1. 通用寄存器 reg : ax、bx、cx、dx、ah、al、bh、bl、ch、cl、dh、dl、sp、bp、si、di；
> 2. 段寄存器 sreg : ds、ss、cs、es

## 8.1 bx、si、di和bp

1. 在 **8086CPU** 中，只有4个寄存器可以用在 `"[..]"` 中来进行内存单元的寻址。例如：
    * `mov ax,[bx]`
    * `mov ax, [bx + si]`
    * `mov ax,[bx + di]`
    * `mov ax,[bp]`
    * `mov ax,[bp + si]`
    * `mov ax, [bp + di]`
2. 在 `[...]` 中，这4个寄存器可以单独出现，或者只能以4种组合出现。例如：
    * `mov ax,[bx]`
    * `mov ax,[si]`
    * `mov ax,[di]`
    * `mov ax,[bp]`
    * `mov ax,[bx + si]`
    * `mov ax,[bx + di]`
    * `mov ax,[bp + si]`
    * `mov ax,[bp + di]`
    * `mov ax,[bx + si + idata]`
    * `mov ax,[bx + di + idata]`
    * `mov ax,[bp + si + idata]`
    * `mov ax,[bp + di + idata]`
3. 只要在 `[...]` 中使用寄存器 bp，而指令中没有显示地给出段地址，段地址就默认在 **ss** 中。例如：
    * `mov ax,[bp]`
    * `mov ax,[bp + idata]`
    * `mov ax,[bp + si]`
    * `mov ax,[bp + si + idata]`

## 8.2 机器指令处理的数据在什么地方

==指令在执行前，所要处理的数据可以出现在3个地方：**CPU内部**、**内存**、**端口**==

|机器码|汇编指令|指令执行前数据的位置|
|:-:|:-:|:-:|
|8E1E000|mov bx,[0]|内存，ds:0 单元|
|89C3|mov bx,ax|CPU内部，ax寄存器|
|BB100|mov bx,1|CPU内部，指令缓冲区|

## 8.3 汇编语言中数据位置的表达

​	在汇编语言中，如果数据在内存中，那么想要定位数据，我们还需要更精确的表达。因为内存是很大的，而想要的数据散布在内存中的各个地方，所以我们需要学会通过汇编语言表达数据的位置。

1. 立即数(idata)

    对于直接包括在机器指令中的数据（执行前在CPU的指令缓冲器中），在汇编语言中称为：立即数(idata)，在汇编指令中直接给出。例如：

    ```
    mov ax,1
    add bx,2000h
    or bx,00010000b
    mov al,'a' ; 会转换成字符对应的 ASCII 码
    ```

2. 寄存器

    指令要处理的数据在寄存器中，在汇编指令中要给出相应的寄存器名。例如：

    ```
    mov ax,bx
    mov ds,ax
    push bx
    mov ds:[0],bx
    push ds
    mov ss,ax
    mov sp,ax
    ```

3. 段地址(SA)和偏移地址(EA)

    指令要处理的数据在内存中，在汇编指令中可用 `[...]` 的格式给出 EA，SA 在某个段寄存器中。

    存放段地址的寄存器可以是默认的，例如：

    ```
    ; 段地址默认存放在 ds 中
    mov ax,[0]
    mov ax,[di]
    mov ax,[bx + 8]
    mov ax,[bx + si]
    mov ax,[bx + si + 8]
    
    ; 段地址默认存放在 ss 中
    mov ax,[bp]
    mov ax,[bp + 8]
    mov ax,[bp + si]
    mov ax,[bp + si + 8]
    
    ; 存放可以显示的给出
    mov ax,ds:[bp]					; (ax) = ((ds)*16 + (bp))
    mov ax,es:[bx]					; (ax) = ((es)*16 + (bx))
    mov ax,ss:[bx + si]			; (ax) = ((ss)*16 + (bx) + (si))
    mov ax,cs:[bx + si + 8]	; (ax) = ((cs)*16 + (bx) + (si) + 8)
    ```

    ==bx 和 bp 不能同时出现，si 和 di 也不能同时出现。只要出现了 bp 段地址默认为 ss ，其他情况下段地址默认存储在 ds 中。==

## 8.4 寻址方式

> ​        当数据存放在内存中的时候，我们可以用多种方式来给定这个内存单元的偏移地址，这种定位内存单元的方法一般被称为寻址方式。

|寻址方式|含义|名称|常用格式举例|
|:-:|:-:|:-:|:--|
|[idata]|EA=idata;SA=(ds)|直接寻址|[idata]|
|[bx]|EA=(bx);SA=(ds)|寄存器间接寻址|[bx]|
|[si]|EA=(si):SA=(ds)|||
|[di]|EA=(di):SA=(ds)|||
|[bp]|EA=(bp);SA=(ss)|||
|[bx + idata]|EA=(bx)+idata;SA=(ds)|寄存器相对寻址|用于结构体：<br/>[bx].idata<br/>用于数组：<br/>idata[si],idata[di]<br/>用于二维数组：<br/>\[bx][idata]|
|[si + idata]|EA=(si) + idata;SA=(ds)|||
|[di + idata]|EA=(di) + idata;SA=(ds)|||
|[bp + idata]|EA=(bp) + idata;SA=(ss)|||
|[bx + si]|EA=(bx) + (si);SA=(ds)|基址变址寻址|用于二维数组：<br/>\[bx][si]|
|[bx + di]|EA=(bx) + (di);SA=(ds)|||
|[bp + si]|EA=(bx) + (si);SA=(ss)|||
|[bp + di]|EA=(bx) + (di);SA=(ss)|||
|[bx + si + idata]|EA=(bx) + (si) + data;SA=(ds)|相对基址变址寻址|用于表格（结构）中的数组项：<br/>[bx].idata[si]<br/>用于二维数组：<br/>idata\[bx][si]|
|[bx + di + idata]|EA=(bx) + (di) + data;SA=(ds)|||
|[bp + si + idata]|EA=(bp) + (si) + data;SA=(ss)|||
|[bp + di + idata]|EA=(bp) + (di) + data;SA=(ss)|||

## 8.5 指令要处理的数据有多长

​    **8086CPU**的指令，可以处理两种尺寸的数据，**byte** 和 **word**。所以在机器指令中也要指明，指令操作的是字操作还是字节操作。有以下几种处理方式。

1. 通过寄存器名指明要处理的数据的尺寸

    ``` 
    ; 寄存器指明了指令进行的是字操作
    mov ax,1
    mov bx,ds[0]
    mov ds,ax
    mov ds:[0],ax
    inc ax
    add ax,1000
    
    ; 寄存器指明了指令进行的是字节操作
    mov al,1
    mov al,bl
    mov al,ds:[0]
    mov ds:[0],al
    int al
    add al,100
    ```

2. 如果在没有寄存器名存在的情况下，用操作符` X ptr` 指明内存单元的长度，**X** 在汇编指令中可以为 **word** 或 **byte**。

    ```
    ; 用 word ptr 指明了指令访问的内存单元是一个字单元
    mov word ptr ds:[0],1
    inc word ptr [bx]
    inc word ptr ds:[0]
    add word ptr [bx],2
    
    ; 用 byte ptr 指明了指令访问的内存单元是一个字节单元
    mov byte ptr ds:[0],1
    inc byte ptr [bx]
    int byte ptr ds:[0]
    add byte ptr [bx],2
    ```

3. 其他方法

    有些指令默认了访问的是字单元还是字节单元，比如，`push [1000H]`就不用指明访问的是字单元还是字节单元，因为 ==push 指令只进行字操作==。

## 8.6 寻址方式的综合应用

![image-20200707235852689](picture/汇编语言(第3版)/image-20200707235852689.png)

问题：需要对这个公司的信息进行修改。

1. Ken Olsen 在富豪榜上的排名已升至 38 位；
2. DEC 的收入增加了 70 亿美元；
3. 该公司的著名产品已变为 VAX 系列计算机。



问题分析：

​		我们需要对对应的内存进行操作，然后修改指定的内容。

		1. (DEC 公司记录)的(排名字段)
  		2. (DEC 公司记录)的(收入字段)
                		3. (DEC 公司记录)的(产品字段)的(第一个字符)、(第二个字符)、(第三个字符)



实现步骤分析：

		1. 首先确定存储 DEC 公司信息的位置是从 `R = seg:60H` 开始的
		2. 然后要修改的第一个字单元的地址为 `R + 0CH`，将财富榜排行从 137 -> 38
		3. 接着要修改的字单元的地址为 `R + 0EH`，将收入增加 40
		4. 然后要修改的是公司著名产品名称，将 "PDP" -> "VAX"，因为占用了多个字节单元，所以我们需要连续修改这三个字节单元。
		5. 第一个字节单元的地址为 `R + 10H`，第二个字节单元的地址为 `R + 10H + 1`，第三个字节单元的地址为 
	 `R + 10H + 2`



关键代码实现如下：

```
mov ax,seg
mov ds,ax
mov bx,60H	; 确认公司信息的记录地址，seg:60H

mov [bx].0CH,38	; 将 R + 0CH 的字单元内容变为 38

mov ax,[bx].0EH
add ax, 40
mov [bx].0EH,ax	; 将 R + 0EH 的字单元内容增加 40

mov si,0
mov [bx].10H[si],'V'
inc si,1
mov [bx].10H[si],'A'
inc si,1
mov [bx].10H[si],'X'; 修改公司著名产品名称为 "VAX"
```

用 C 语言关键代码实现如下：

```c
struct company { 	// 定义一个公司记录的结构体
		char cn[3];		// 公司名称
  	char hn[9];		// 总裁姓名
  	int pm;				// 排名
  	int sr;				// 收入
  	char cp[3];		// 著名产品		
};
struct company dec = {"DEC","Ken Olsen",137,40,"PDP"};

int main(void)
{
	  int i;
		dec.pm = 38;
  	dec.sr = dec.sr + 70;
  	i = 0;
  	dec.cp[i] = 'V';
  	i ++;
  	dec.cp[i] = 'A';
  	i ++;
  	dec.cp[i] = 'X';
  	
  	return 0;
}
```



代码比较

| 作用                         | 汇编指令实现                                                 | C语言实现                                                    |
| ---------------------------- | ------------------------------------------------------------ | ------------------------------------------------------------ |
| 修改排名为 38                | `mov [bx].0CH,38	`                                        | `dec.pm = 38;`                                               |
| 收入增加 40                  | `mov ax,[bx].0EH`<br />`add ax, 40`<br />`mov [bx].0EH,ax`   | `dec.sr = dec.sr + 70;`                                      |
| 修改公司著名产品名称为 "VAX" | `mov si,0`<br />`mov [bx].10H[si],'V'`<br />`inc si,1`<br />`mov [bx].10H[si],'A'`<br />`inc si,1`<br />`mov [bx].10H[si],'X'` | `i = 0;`<br/>`dec.cp[i] = 'V';`<br/>`i ++;`<br/>`dec.cp[i] = 'A';`<br/>`i ++;`<br/>`dec.cp[i] = 'X';` |

**小结**：通过对汇编指令实现和C语言实现进行对比。我们发现 8086CPU 提供的如 [bx + si + idata]的寻址方式，为结构化数据的处理提供了方便。并且，我们可以将其写为更为贴切的形式，如：[bx].idata、[bx].idata[si]。





## 8.7 div指令

div 是汇编指令中的除法指令，在使用这个指令的时候，我们应该注意以下问题。

> 被除数 / 除数 = 商 ... 余

1. 除数：有 8 位和 16 位两种，在一个 reg 或内存单元中。
2. 被除数：默认放在 **AX** 或 **(DX 和 AX)** 中。
    * 如果除数为 8 位，被除数则为 16 位，默认在 AX 中存放；
    * 如果除数位 16 位，被除数则为 32 位，在 DX 和 AX 中存放，DX 存放高 16 位，AX 存放低 16 位。
3. 结果：
    * 如果除数为 8 位，则 AL 存储除法操作的商，AH 存储除法操作的余数；
    * 如果除数为 16 位，则 AX 存储除法操作的商，DX 存储除法操作的余数。

格式如下：

```
; 格式一：div reg
; 格式二：div 内存单元
```



问题1：编程，利用除法指令计算 100001/100。

```
; 问题分析
; 第一我们需要确定除数和被除数的长度，因为这个决定了，除数和被除数存放的地址
; 虽然除数的只需要在 8 位寄存器中就能存储，理论上我们应该将被除数存储在 AX 中
; 但是被除数的大小是大于 65535 的，所以我们需要 32 位长度来进行存储
; 最后得出的结论是 除数存储 BX 中，被除数的 低16位 存储在 AX 中，高16位 存储在 DX 中
; 关键代码实现如下：

mov dx,1
mov ax,86A1H ; (dx)*10000H + (ax) = 100001
mov bx,100
div bx

; 运行结果 (ax) = 03E8H(商为 100)，(dx) = 1H(余数为 1)
```



问题2：编程，利用除法指令计算 1001/100。

```
; 问题分析
; 还是先确定除数和被除数的长度，决定如何将其进行存放
; 因为 100 < 255，所以除数只需要占用 8 位的长度
; 因为 1001 < 65535，所以被除数只需要占用 16 位的长度
; 结论将被除数放在 AX 中，将除数放在 BL 中
; 关键代码实现如下：

mov ax,1001
mov bl,100
div bl

; 运行结果 (al) = 0AH(商为 10)，(ah) = 01H(余数为 1)
```



## 8.8 伪指令 dd

之前已经学过了在代码中使用 db 和 dw。而 **dd** 是用来定义 **dword** (double word，双字)型数据的。例如：

```
data segment
		db 1	; 数据为 01H，在 data:0 处，占一个字节
		dw 1	; 数据为 0001H，在 data:1 处，占两个字节，即一个子
		dd 1	; 数据为 00000001H，在 data:3 处，占四个字节，即两个子
data ends
```



### 问题 8.1

​		用 div 计算 data 段中第一个数据除以第二个数据后的结果，商存在第三个数据的存储单元中。

```
data segment
		dd 100001
		dw 100
		dw 0
data ends
```

分析：

因为是除法，所以我们首先需要分析，这两个数据的在运算过程中所占的长度。被除数 100001 > 65535 所以需要两个字单元存储；除数 100 > 255 所以一个字节单元就能存储，但由于被除数是两个字单元存储，所以除数也需要一个字单元存储。代码实现如下：

```
assume cs:code ds:data

data segment
		dd 100001
		dw 100
		dw 0
data ends

code segment
start:mov ax,data
			mov ds,ax ;初始化段寄存器 ds
			
			mov ax,[0] ;将被除数的 低16位 存放到 ax 中
			mov dx,[2] ;将被除数的 高16位 存放到 dx 中
			
			div word ptr [4] ;进行计算
      
      mov [6],ax ;将商存储到指定的位置

code ends

end start
```

## 8.9 dup

​		**dup** 是一个操作符，在汇编语言中同 db、dw、dd 等一样，也是由编译起识别处理的符号。它是和 db、dw、dd 等数据定义伪指令配合使用的，用来进行数据的重复的。在之前的学习中，如果我们要定义 100 个字节单元的话，就需要写 db 3 0,0,0,0,0,0,....(100 个 0)，但是使用了 dup 的话，就可以很方便表示了 db 100 dup(0)。

​		格式如下：

​		db 重复的次数 dup (重复的字节型数据)

​		dw 重复的次数 dup (重复的字型数据)

​		dd 重复的次数 dup (重复的双字型数据)

例如：

```
data segment
		db 3 dup (0)
		; 定义了 3 个字节，相当于 db 3 0,0,0
		
		db 3 dup (0,1,2)
		; 定义了了 9 个字节单元，相当于 db 9 0,1,2,0,1,2,0,1,2
		
		db 3 dup ('abc','ABC')
		; 定义了 18 个字节单元，相当于 db 'abcABCabcABCabcABC'
data ends
```



### 实验 7 寻址方式在结构化数据访问中的应用

| 年份 | 收入(千美元) | 雇员(人) | 人均收入(千美元) |
| :--: | -----------: | -------: | :--------------: |
| 1975 |           16 |        3 |        ?         |
| 1976 |           22 |        7 |        ?         |
| 1977 |          382 |        9 |        ?         |
| 1978 |         1356 |       13 |        ?         |
| 1979 |         2390 |       28 |        ?         |
| 1980 |         8000 |       38 |        ?         |
| ...  |              |          |        ?         |
| 1995 |      5937000 |    17800 |        ?         |

Power idea 公司从 1975 年成立一直到 1995 年的基本情况，按照如上格式进行存放到内存中。完整数据如下；

```
assume cs:codesg ds:data

data segment
	db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
	db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
	db '1993','1994','1995'
	; 年份
	
	dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
	dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000
	; 每年的收入
	
	dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258.2793,4037,5635,8226
	dw 11542,14430,15257,17800
	; 雇员人数
data ends

table segment
	db 21 dup ('year summ ne ?? ')
table ends
```

按照如下的格式保存在 table 段中。

![image-20200708202725764](picture/汇编语言(第3版)/image-20200708202725764.png)

分析：

首先分析定义的数据，可以看出，抽象的看，有三种数组，各包含了 21 条数据。

​		年份数组的起始地址为：data:[0]

​		收入数组的起始地址为：data:[84]

​		雇员人数的起始地址为：data:[168]

​		寻址格式可以抽象为：data:idata\[bx][si]，idata 表示数组的起始地址，bx 表示每条数据的起始地址，si 表示如果每条数据如果是数组格式用来遍历这个数组。

再分析目的内存，可以抽象为一个数组，数组里面存储的是一个结构体

​		年份：table:\[bx].0[si]

​		收入：table:[bx].5[si]

​		雇员人数：table:[bx].10

​		人均收入：table:[bx].13

**代码实现如下**：

```
assume cs:codesg,ds:data,ss:stack

data segment
  db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
  db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
  db '1993','1994','1995'
  ; 年份
  
  dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
  dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000
  ; 每年的收入
  
  dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
  dw 11542,14430,15257,17800
  ; 雇员人数
  
  dw 0,0,0
  ; 记录每个数组当前的下标，对应的起始偏移地址为 210、212、214
data ends

table segment
  db 21 dup ('year summ ne ?? ')
table ends

stack segment
  dw 10 dup (0)
stack ends

codesg segment
start:mov ax,data
      mov ds,ax   ;将 data 的段地址放在 ds 中
      
      mov ax,table
      mov es,ax   ;将 table 的段地址放在 es 中
      
      mov ax,stack
      mov ss,ax   
      mov sp,20H  ;初始化栈
      
      mov cx,21
      mov bp,0
   s0:push cx     ;遍历 data
      
      mov bx,ds:[210]
      mov si,0
      mov di,0
      mov cx,4
   s1:mov al,0[bx][si]    ;获取年份
      mov es:[bp].0[di],al  ;存储年份到 table 中
      inc si
      inc di
      loop s1
      add bx,4
      mov ds:[210],bx
      ;存储年份
      
      mov cx,2
      mov si,0
      mov di,0
      mov bx,ds:[212]
   s2:mov ax,84[bx][si]
      push ax             ; 将年收入放到栈中，方便计算平均收入
      mov es:[bp].5[di],ax
      add si,2
      add di,2
      loop s2
      add bx,4
      mov ds:[212],bx
      ;存储年收入
      
      mov bx,ds:[214]
      mov ax,168[bx]
      mov es:[bp].10,ax
      add bx,2
      mov ds:[214],bx
      ;存储雇员人数
      
      mov bx,ax
      pop dx              ;高16位存储在 dx 中
      pop ax              ;低16位存储在 ax 中
      div bx              ;计算得到平均收入
      mov es:[bp].13, ax
      ;计算平均收入并存储
      
      add bp,16           ;指向 table 的下一行
      pop cx
      loop s0

      mov ax,4c00H
      int 21
codesg ends

end start
```



# 第 9 章 转移指令的原理

​		可以修改 **IP**，或同时修改 **CS** 和 **IP** 的指令统称为转移指令。概括的讲，转移指令就是可以控制 CPU 执行内存中某处代码的指令。

根据 8086CPU 的转移行为，可进行如下分类

![image-20200708234610995](picture/汇编语言(第3版)/image-20200708234610995.png)

8086CPU 的转移指令分为一下几类

* 无条件转移指令 (jmp)
* 条件转移指令
* 循环指令 (loop)
* 过程
* 中断

## 9.1 操作符 offset

​		操作符 **offset** 在汇编语言中是由编译器处理的符号，它的功能是取得标号的偏移地址。例如：

```
assume cs:codesg

codesg segment

	start:mov ax,offset start		;相当于mov ax,0
			s:mov ax,offset s				;相当于mov ax,3
			
codesg ends

end start
```

### 问题 9.1

​		有如下程序段，填写两条指令，使该程序运行中将s处的一条指令复制到s0处。

```
assume cs:codesg

codesg segment
    s:  mov ax,bx
        mov si, offset s
        mov di, offset s0
        mov ax, cs:[si]
        mov cs:[di],ax
    s0: nop
        nop
codesg ends

end s
```

## 9.2 jmp 指令

**jmp** 为无条件转移指令，可以只修改 IP，也可以同时修改 CS 和 IP。

**jmp** 指令要给出的两种信息：

1. 转移的目的地址
2. 转移的距离（段间转移、段内短转移、段内近转移）

## 9.3 根据位移进行转移的 jmp 指令

### jmp short 标号(转到标号处执行指令)



这种格式的 jmp 指令实现的是段内短转移，它对 IP 的修改范围为 -128~127。也就是说，它向前转移时最多可以越过 128 个字节，向后转移可以最多越过 127 个字节。转移指令结束后，CS:IP 就会指向标号处的指令。例如；

```
assume cs:codesg

codesg segment

	start:mov ax,0
				jmp short s
				mov ax 				;这条语句不会被执行
			s:inc ax

codesg ends

end start
; 最终 ax = 1
```

#### "jmp short 标号" 如何跳转到目的地址



| 代码                                                         | 对应的机器码                                                 |
| ------------------------------------------------------------ | ------------------------------------------------------------ |
| start:mov ax,0<br/>		jmp short s<br/>		mov ax <br/>	 s:inc ax | ![image-20200709002634370](picture/汇编语言(第3版)/image-20200709002634370.png) |
| start:mov ax,0<br/>        mov bx,0<br/>        jmp short s<br/>        add ax,1<br/>     s:inc ax | ![image-20200709002942205](picture/汇编语言(第3版)/image-20200709002942205.png) |

通过观察上面的表格，我们可以发现代码发生了修改，导致了跳转的地址也发生了修改。代码一为 `jmp 0008H`，代码二为 `jmp 000BH`，但是对应的机器码却没有发生改变，都是 ***EB03***，说明 ==CPU 在执行 jmp 指令的时候并不需要转移的目的地址==。那么 CPU 这么知道跳转的地址是什么呢，CPU 是死的，不可能猜测到我们的想法是啥，所以肯定在某个地方直接或间接的告诉了 CPU。

> 在转移指令 **EB 03** 中并没有告诉 CPU 要转移的目的地址，但是告诉了 CPU 要转移的位移，即将当前的 IP 向后移动 3 个字节。因为程序1、2中的 jmp 指令转移的位移相同，都是向后 3 个字节，所以它们的机器码都是 **EB 03**。

例如：

![image-20200709005351514](picture/汇编语言(第3版)/image-20200709005351514.png)

实际上，=="jmp short 标号"的功能为：(IP) = (IP) + 8 位位移(即位移的范围为8位)==

1. 8 位位移=标号处的地址 - jmp 指令后的第一个字节的地址；
2. short 指明此处的位移为 8 位位移；
3. 8 位位移的范围为 -128~127，用补码表示；
4. 8 位位移由编译程序在编译时算出。

比如：

1. 图中红色 JMP，07H - 03H = 04H，所以对应的机器码为 EB**04**H；
2. 图中黄色 JMP，00H - 08H = F6H (由补码进行表示)，所以对应的机器码为 EB**F6**H。

### jmp near ptr 标号

`jmp near ptr 标号`,它实现的是段内近转移，原理和 `jmp short 标号`类似。

=="jmp near ptr 标号" 的功能为：(IP) = (IP) + 16 位位移==

1. 16 位位移=标号处的地址 - jmp 指令后的第一个字节的地址；
2. near ptr 指明此处的位移为 16 位位移；
3. 16 位位移的范围为 -32768~32767，用补码表示；
4. 16 位位移由编译程序在编译时算出。



## 9.4 转移的目的地址在指令中的 jmp 指令

`jmp far ptr 标号`实现的是段间转移，又称为远转移。`far prt 标号`指明了指令用标号的段地址和偏移地址。让 CPU 知道目的地址在什么地方。

```
assume cs:codesg

	codesg segment
		start:mov ax,0
					mov bx,0
					jmp far ptr s
					db 256 dup (0)
				s:add ax,1
					inc ax
	codesg ends
```

![image-20200709172824385](picture/汇编语言(第3版)/image-20200709172824385.png)

通过上图可以看到，jmp far ptr s 对应的机器码为 EA0B016A07。其中机器码 **EA** 代表这是一个段间转移指令，目的地址为 **010B 076AH**（由于高地址在内存中存储在高位，所以在表示书写时，应该将高位的放在地位之前）。

## 9.5 转移地址在寄存器中的 jmp 指令

指令格式：jmp 16 位 reg

功能：(IP)=(16 位 reg)

## 9.6 转移地址在内存中的 jmp 指令

1. jmp **word** ptr 内存单元地址(段内转移)

    功能：从内存单元地址处开始存储着一个字，是转移的目的编译地址。内存单元地址可以用寻址方式的任一格式给出。例如：

    ```
    mov ax,0123H
    mov ds:[0],ax
    jmp word ptr ds:[0]
    ```

2. jmp **dword** prt 内存单元地址(段间转移)

    功能：从内存单元地址处开始存放着两个字，高地址处的字是转移的目的段地址，低地址处是转移的目的偏移地址。

    (CS) = (内存单元地址 + 2)

    (IP) = (内存单元地址)

    例如：

    ```
    mov ax,0123H
    mov [bx],ax
    mov word ptr [bx+2],0
    mov dword ptr [bx]
    ; 执行后 (CS)=0，(IP)=0123H，CS:IP指向 0000:0123
    ```

### 测试点 9.1

1. 使程序中的 jmp 指令执行后，CSL:IP 指向程序的第一条指令，在 data 中应该定义哪些数据？程序如下。

    ```
    assume cs:code
    
    data segment
    	?							;db 3 dup (0)
    data ends
    
    code segment
    
    	start:mov ax,data
    				mov ds,ax
    				mov bx,0
    				jmp word ptr [bx+1]
    
    code ends
    
    end start
    ```

    分析：转移指令格式为 `jmp word ptr 内存单元地址` ，为段内短转移。因为要跳转到程序的第一条指令，所以内存单元地址中存储的字单元值必须为 0000H，而这个值是从 data:1 的位置获取到的字单元，所以只要 data 中定义数据的长度超过 3 字节，并且 data 中的内容为 ?? 00 00 ...，就能满足如上的要求。

2. 补全程序，使 jmp 指令执行后，CS:IP 指向程序的第一条指令。程序如下：

    ```
    assume cs:code
    
    data segment
    	dd 12345678H
    data ends
    
    code segment
    	
    	start:mov ax,data
    				mov ds,ax
    				mov bx,0
    				mov [bx],?						; 0000H
    				mov [bx+2],?					; cs
    				jmp dword ptr ds:[0]
    				
    code ends
    
    end start
    ```

    分析：转移指令格式为 `jmp dword ptr ds:[0]`，为段间转移指令。因为要跳转到程序的第一条指令，所以要**同时的给出段地址和偏移地址**，要将段地址存放在 ds:[2] 的字单元中，将偏移地址存放在 ds:[0]字单元中，即 data:[0] = 0000H，data:[2] = cs。

3. 用 Debug 查看内存，结果如下：

    2000:1000 BE 00 06 00 00 00 ......

    则此时，CPU 执行指令：

    ```
    mov ax,2000H
    mov es,ax
    jmp dword ptr es:[1000H]
    ```

    后，(CS)=？，(IP)=？

    

    分析：

    低端地址存储的是偏移地址，高端地址存储的是段地址，所以 (CS) = 0006H，(IP) = 00BEH。

## 9.7 jcxz 指令

**jcxz** 指令为有条件转移指令，所有的有条件转移指令都是短转移，在对应的机器码中包括转移的位移，而不是目的地址。对 IP 的修改范围都为：-128~127。

指令格式：jcxz 标号（如果(cx)=0，转移到标号处执行。）
操作：当(cx) = 0时，(IP)=(IP)+8位位移；

​	8 位位移 = 标号处的地址 - jcxz 指令后到第一个字节的地址

​	8 位位移的范围为 -128~127，用补码表示；

​	8 位位移由编译器在编译时计算。

当(cx) != 0 时，什么也不做(程序向下执行)。

用 C 语言的方式表示为 if ((cx) == 0) jmp short 标号;

### 检测点 9.2

补全编程，利用 jcxz 指令，实现在内存 2000H 段中查找第一个值为 0 的字节，找到后，将它的偏移地址存储在 dx 中。

```
assume cs:code
code segment
	start:mov ax,2000H
				mov ds,ax
				mov bx,0
			s:?0							; mov cl,[bx]
				?1							; and cx,0FH
				?2							; jcxz ok
				?4							; inc bx
				jmp short s
		 ok:mov dx,bx
		 		mov ax,4c00h
		 		int 21h
code ends
end start
```

分析：首先要查找内存 2000H 段中的第一个值为 0 的字节，所以要首先要得到对应内存中的值，然后存放到 cx 中，但是因为是一个一个字节进行遍历，如果找到对应字节的值为 0 的，则跳过 `jmp short s`指令，直接执行下面的指令，所以可以确定，第三个问号为 `jcxz ok`，第四个问号为 `inc bx`。但是 **jcxz** 指令是根据 **cx** 来进行判断的，所以要将对应的内存字节单元的值放到 **cx** ，不过因为是一个字节一个字节取得，因此只需要用到低 8 位，而高 8 位的内容可能会影响判断，就需要对这个 cx 进行处理，让高 8 位置零(通过 & 操作)。由此可以得出第一个问号为 `mov cl [bx]`，第二个问号为`and cx,0FH`。（顺序上可以做调整，答案不唯一）



## loop 指令

loop 指令为循环指令，所有的循环操作都是 **短转移**，在对应的机器码中包含转移的位移，而不是目的地址。对 **IP** 的修改范围都为：-128~127。

***指令格式***：`loop 标号`((CX) = (CX) - 1，如果 (CX )!= 0，转移到标号处执行)

操作：

1. (CX) = (CX) - 1;

2. 如果 (CX) != 0，(IP) = (IP) + 8 位位移；

    8 位位移 = 标号处的地址 - loop 指令后的第一个字节的地址；

    8 位位移的范围为 -128~127，用补码表示；

    8 位位移由编译器在编译时算出。

3. 如果 (CX) = 0，什么也不做（程序向下执行，不跳转到标号处）。

### 检测点 9.3

补全编程，利用 loop 指令，实现在内存中 2000H 段中查找第一个值为 0 的字节，找到后，将它的偏移地址存储到 dx 中。程序如下：

```
assume cs:code

code segment

	start:mov ax,2000H
				mov ds,ax
				mov bx,0
			s:mov cl,[bx]
				mov ch,0
				?							;inc cx
				inc bx
				loop s
		 ok:dec bx				;dec 指令的功能和 inc 指令相反，dec bx 进行的操作：(BX) = (BX) - 1
		 		mov dx,bx
		 		mov ax,4c00h
		 		int 21
		 		
code ends
end start
```

分析：因为`loop s` 判断是否执行是通过 **cx** 的值，只有在 cx = 0 的情况下程序才会继续像下进行。分析 loop 指令的执行，是先将 (cx) = (cx) - 1，然后再判断 cx 是否为 0，所以我们要保证在内存字节单元为 0 的时候，在 cx 中要表示为 1，这样，才能正确的退出，所以将 cx 的值先自增 1，即问号处可以填 `inc cx` 或 `add cx,1`。

## 9.9 根据位移进行转移的意义

1. jmp short 标号
2. jmp near ptr 标号
3. jcxz 标号
4. loop 标号

等几种汇编指令，它们对 IP 的修改是根据转移目的地址和转移起始地址之间的位移来进行的。在它们对应的机器码中不包含转移的目的地址，而包含的是到目的地址的位移。==这样设计的好处是，相同的程序，装在内存中的不同位置都可正确执行，因为在执行的过程中只涉及位移，而不是地址，位移在代码不做改变的情况下是不会改变的==。



## 9.10 编译器对转移位移超界的检测

根据位移进行转移的指令，它们的转移范围受到转移位移的限制，如果在源程序中出现了转移范围超界的问题，在编译的时候，编译器在编译的时候将报错。

### 实验 8 分析一个奇怪的程序

分析下面的程序，在运行前思考：这个程序可以正确返回吗？

运行后再思考：为什么是这种结果？

```
assume cs:code

code segment

       mov ax,4c00h
       int 21h

start: mov ax,0
    s: nop
       nop

       mov di,offset s
       mov si,offset s2
       mov ax,cs:[si]
       mov cs:[di],ax			;将 s2 的那行指令复制到了 s 处

   s0: jmp short s

   s1: mov ax,0
       int 21h
       mov ax,0

   s2: jmp short s1
       nop

code ends
end start
```

回答1：可以正确的返回

回答2：因为将 s2 的 `jmp short s1` 复制到了 s 处。即，将 s 处对应的机器码变为了 **EBF6**，就是在执行这步以后，将 IP 移动 F6 个字节，将 F6(补码)，转换为原码就是 -10，所以就向前移动 10 个字节，刚好指向了 code 代码段中的第一行指令，所以可以正常的返回。

### 实验 9 根据材料编程

**前置知识：80 * 25 彩色字符模式显示缓冲区（以下简称显示缓冲区）的结构**

显示缓冲区在内存地址空间中的范围为 B8000H~BFFFFFH 共 32KB 的空间，为 80*25 彩色字符模式的显示缓冲区。向这个地址空间写入数据，写入的内容将立即出现在显示器上。

80*25 彩色字符模式，显示器可以显示 25 行，没行 80 个字符，每个字符可以有 256 种属性（背景色、前景色，即字符颜色、闪烁、高亮等组合信息）。

> 属性字节的格式：
>
> ​				7	  6	5	4	3	2	1	0
> 含义		BL	R	G	B	I	R	G	B
>
> ​			闪烁		背景  	高亮		前景
>
> R: 红色
>
> G: 绿色
>
> B: 蓝色
>
> 例如：
>
> 红色绿字，属性字节为：01000010B
>
> 红底闪烁绿字，属性字节为：11000010B
>
> 红底高亮绿字，属性字节为：01001010B
>
> 黑底白字，属性字节为：00000111B
>
> 白底蓝字，属性字节为：01110001B

一个字符在显示缓冲区中占两个字节，低位字节存储字符的 ASCII 码，高位字节存储字符的属性。80*24 模式下，一屏的内容在显示缓冲中共占 4000 个字节（约 4KB）。由于显示缓冲区总共有 32KB 的空间，所以缓冲区被分为了 8 页，一般来说显示第 0 页第内容。也就是 B80000H~B8F9FH 中的 4000 个字节的内容出现在显示屏上。

例如：

B800:000 41 ca 42 ca 43 ca 44 ca 45 ca 46 ca

![image-20200710005634962](picture/汇编语言(第3版)/image-20200710005634962.png)

可以看到在 DOS 窗口的左上角出现了彩色字符



***编程实战：在屏幕中间分别显示绿色、绿底红色、白底蓝色的字符串'welcome to masm!'。***

完整代码如下：

```
assume cs:code

data segment
    db 'welcome to masm!'
    db 02H,24H,71H
data ends

stack segment
    db 16 dup (0)
stack ends

code segment

start:  mov ax,data
        mov ds,ax								;将 data 段地址存到 ds 中

        mov ax,stack
        mov ss,ax
        mov sp,10H							;初始化堆栈

        mov ax,0b800H
        mov es,ax
        mov bx,1760							;初始化显示的位置


        mov cx,3								;3 行
        mov di,0								;每行的颜色的偏移地址
     s0:push cx

        mov cx,16								;16 个字符
        mov si,0								;显示缓冲区每个字符的起始偏移地址
        mov bp,0								;代表 data 中字符的偏移地址
     s1:mov al,ds:[bp]					;字符的对应的 ASCII
        mov ah,16[di]						;字符的属性，即颜色
        mov es:[bx].64[si],ax
        add si,2
        inc bp
        loop s1

        add bx,160							;换行
        inc di									;换颜色
        pop cx
        loop s0

        mov ax,4c00H
        int 21h

code ends

end start
```



# 第 10 章 CALL 和 RET 指令

**call 和 ret 指令都是转移指令，他们都修改 IP，或同时修改 CS 和 IP。它们经常被共同用来实现子程序的设计。**

## 10.1 ret 和 retf

| 汇编指令 | 作用                                                     | 进行的操作                                                   | 通俗解释           |
| :------: | :------------------------------------------------------- | ------------------------------------------------------------ | ------------------ |
|   ret    | 用栈中的数据，修改 IP 的内容，<br />从而实现近转移       | 1. (IP)=((ss)*16 + (sp))<br />2. (sp)=(sp) + 2               | pop IP             |
|   retf   | 用栈中的数据，修改 CS 和 IP <br />的内容，从而实现远转移 | 1. (IP)=((ss)*16 + (sp))<br />2.(sp)=(sp) + 2<br />3. (CS)=((ss)*16 + (sp))<br />4. (sp)=(sp) + 2 | pop IP<br />pop CS |

### 检测点 10.1

补全程序，实现从内存 1000:0000 处开始执行指令。程序如下：

```
assume cs:code

stack segment
	db 16 dup (0)
stack ends

code segment
	start: mov ax,stack
		   mov ss,ax
		   mov sp,16
		   ?				;mov ax,1000H
		   push ax
		   ?				;mov ax,0
		   push ax
		   retf
	
code ends

end start
```

## 10.2 call 指令

CPU 执行 call 指令时，进行两步操作：

1. 当前的 IP 或 CS 和 IP 压入栈中；
2. 转移。

call 指令不能实现短转移，实现转移方法的原理和 jmp 指令的原理相同。



## 10.3 依据位移进行转移的 call 指令

call 指令格式：call 标号

CPU 执行这种格式的 call 指令时，进行两部操作：

1. (sp) = (sp) - 2

    ((ss)*16 + (sp)) = (IP)

2. (IP) = (IP) + 16 位位移。

    16 位位移 = 标号处的地址 - call 指令后的第一个字节的地址；

    16 位位移的范围为 -32768~32767，用补码表示；

    16 位位移由编译程序在编译时算出。



### 检测点 10.2

下面的程序执行后，ax 中的数值为多少？

```
内存地址	机器码			汇编指令
1000:0		b8 00 00	mov ax,0
1000:3		e8 01 00	call s
1000:6		40			inc ax
1000:7		58			s:pop ax
; 程序运行结束后，ax = 6
```



## 10.4 转移的目的地址在指令中的 call 指令

上面提到的 call 指令，其对应的机器码中没有转移的目的地址，而是相对于当前 IP 的转移位移。

`call far ptr 标号` 实现的是段间转移

CPU 执行此种格式的 call 指令时，进行如下的操作。

1. (sp) = (sp) - 2

    ((ss)*16 + (sp)) = (CS)

    (sp) = (sp) - 2

    ((ss)*16 + (sp)) = (IP)

2. (CS) = 标号所在段的段地址

    (IP) = 标号所在段中的偏移地址

用汇编语法来进行解释：

push CS

push IP

jmp far ptr 标号



### 检测点 10.3

下面的程序执行后，ax 中的数值为多少？

```
内存地址		机器码					汇编指令
1000:0			b8 00 00 			mov ax,0
1000:3			9a 09 00 00 10		call far ptr s
1000:8			40					inc ax
1000:9			58			  	  s:pop ax
									add ax,ax
									pop bx
									add ax,bx
; 程序结束后 ax = 1012H
```



## 10.5 转移地址在寄存器中的 call 指令

指令格式：call 16 位 reg

功能：

(sp) = (sp) - 2

((ss)*16 + (sp) = (IP)

(IP) = (16 位 reg)

用汇编语言来解释：

​	push IP

​	jmp 16 位 reg



### 检测点 10.4

下面的程序指令后，ax 中的数值为多少？

```
内存地址		机器码				汇编指令
1000:0			b8 06 00		mov ax,6
1000:3			ff	d0			call ax
1000:5			40				inc ax
1000:6							mov bp,sp
								add ax,[bp]
;程序结束后，ax = 5
```



## 10.6 转移地址在内存中的 call 指令

转移地址在内存中的 call 指令有两种格式。

1. call word ptr 内存单元地址

    用汇编语法解释：

    push IP

    jmp word ptr 内存单元地址

2. call dword ptr 内存单元地址

    用汇编语法解释：

    push CS

    push IP

    jmp word ptr 内存单元地址



### 检测点 10.5

1. 下面的程序执行后，ax 中的数值为多少？

    ```
    assume cs:code
    
    stack segment
    	dw 8 dup (0)
    stack ends
    
    code segment
    
    	start:	mov ax,stack
    			mov ss,ax
    			mov sp,16				;初始化堆栈地址
    			
    			mov ds,ax
    			mov ax,0
    			call word ptr ds:[0EH]
    			inc ax
    			inc ax
    			inc ax
    			
    			mov ax,4c00h
    			int 21h
    			
    code ends
    
    end start
    
    ;程序结束后，ax = ？因为在执行 mov ss,ax 时，会初始化堆栈中的值
    ;然后在执行 call 指令时会先进行跳转，然后再将对应的 IP 存储到堆栈中
    ;所以跳转的地方时未知的
    ```

2. 下面的程序执行后，ax 和 bx 中的数值为多少？

    ```
    assume cs:code
    
    data segment
    	dw 8 dup (0)
    data ends
    
    code segment
    	start:	mov ax,data
    			mov ss,ax
    			mov sp,16
    			
    			mov word ptr ss:[0],offset s
    			mov ss:[2],cs
    			call dword ptr ss:[0]
    			nop
    		s:	mov ax,offset s
    			sub ax,ss:[0cH]
    			mov bx,cs
    			sub bx,ss:[0eH]
    			
    			mov ax,4c00h
    			int 21h
    			
    code ends
    
    end start
    
    ;程序结束后 ax = 1，bx = 0
    ```



## 10.7 call 和 ret 的配合使用

配合 ret 和 call 指令的原理，实现子程序的机制。

### 问题 10.1

下面程序返回前，bx 中的值是多少？

```
assume cs:code

code segment
	start:	mov ax,1
			mov cx,3
			call s			;将下一条指令的偏移地址压入堆栈，并跳转到 s 处
			mov bx,ax
			mov ax,4c00h
			int 21h
		s:	add ax,ax
			loop s
			ret				;弹栈，将得到的值设置为 IP，即下一条指令的偏移地址

code ends

end start

;程序结束后 bx = 8
```



## 10.8 mul 指令

mul 指令，即乘法指令，使用 mul 做乘法的时候，注意一下两点。

1. 两个相乘的数，要么都是 8 位，要么都是 16 位，如果是 8 位，一个默认放在 AL 中，另一个放在 8 位 reg 或内存字节单元中；如果是 16 位，一个默认在 AX 中，另一个放在在 16 位内存字单元中。
2. 结果：如果是 8 位乘法，结果默认放在 AX 中；如果是 16 位乘法，结果高位默认在 DX 中存放，低位在 AX 中存放。

格式如下：

mul reg

mul 内存单元（可以通过各种方式给出，例如：mul byte ptr ds:[0]）



例如：

1. 计算 100 * 10

    mov al,100

    mov bl,10

    mul bl

2. 计算 100 * 10000

    mov ax,100

    mov bx,10000

    mul bx



## 10.10 参数和结果传递的问题

子程序一般都需要根据提供的参数处理一定的事务，处理后，将结果（返回值）提供给调用者。那么我们应该如何存储子程序需要的参数和产生的返回值呢。

1. 通过寄存器来进行存储，优点是方便，缺点是存储的数据有限；
2. 通过内存单元来进行存储，优点能存储多个数据
3. 通过堆栈进行存储（通用）

例如：计算 data 段中第一组数据的 3 次方，结果保存在后面的一组 dword 单元中（通过子程序实现，利用寄存处传递数据）。

```
assume cs:code

data segment
	dw 1,2,3,4,5,6,7,8
	dd 8 dup (0)
data ends

code segment
	
	
	start:	mov ax,data
			mov ds,ax
			mov si,0			;data 段中第一组 word 单元
			mov di,16			;data 段中第二行 dword 单元
			
			mov cx,8
		s:	mov bx,[si]
			call cube
			mov [di],bx
			mov [di].2,dx
			add si,2			;ds:di 指向下一个 word 单元
			add di,4			;ds:si 指向下一个 dword 单元
			loop s
			
			mov ax,4c00h
			int 21h
			
			;计算 N 的 3 次方
			;参数：(bx) = N
			;结果：(dx:ax) = N^3
	cube:	mov ax,bx
			mul bx
			mul bx
			ret

code ends

end start
```



## 10.11 批量数据的传递

通过内存单元来进行传递，也可以使用堆栈。



例如：将 data 段中的字符串转化为大写（利用子程序实现，通过内存单元传递数据）。

```
assume cs:code

data segment
	db 'conversation'
data ends

code segment

	start:	mov ax,data
			mov ds,ax
			
			mov si,0
			mov cx,12
			call capital
			
			mov ax,4c00h
			int 21h
			
  capital:	and byte ptr [si],11011111B
  			inc si
  			loop capital
  			ret
code ends

end start
```



## 10.12 寄存器冲突的问题

**什么是寄存器冲突**：子程序中使用的寄存器，很可能在主程序中也要使用，造成了寄存器使用上的冲突。

最简单的解决方案有两种：

1. 在编写调用子程序的程序时，注意看看子程序中有没有用到会冲突的寄存器，如果有，调用者使用别的寄存器；
2. 在编写子程序的时候，不要使用会产生冲突的寄存器。

但是以上两种方案都不太合理，在理想状态下，子程序的实现应该是黑盒的，即调用者不应该关系这个程序是怎么实现的，如果每次调用子程序都需要观察子程序中使用寄存器的情况，然后来避免和子程序冲突的话，那么这种设计是很不合理的，而且肯定会存在没办法避免的冲突。例如：调用者和子程序中都有 loop 指令。



**子程序设计准则**：

1. 编写调用子程序的程序的时候不必关心子程序到底是用了哪些寄存器；
2. 编写子程序的时候不必关心调用者使用了哪些寄存器；
3. 不会发生寄存器的冲突。

要满足以上 3 点，我们就要在子程序真正开始处理数据之前，将要使用的寄存器的状态进行保存，在子程序返回之前再恢复状态。如下：

​	子程序开始:	子程序中使用的寄存器入栈

​							子程序内容

​							子程序中使用的寄存器出栈

​							返回



### 实验 10 编写子程序

#### 1. 显示字符串

**问题**：编写一个通用的子程序来实现显示字符串的功能。提供灵活的调用接口，使调用者可以决定显示的位置（行、列）、内容和颜色。

**子程序描述**：

​	名称：show_str

​	功能：在指定的位置，用指定的颜色，显示一个用 0 结束的字符串。

​	参数：(dh) = 行号(取值范围 0~24)，(dl) = 列号(取值范围 0~79)，(cl) = (颜色)，ds:si 指向字符串的首地址

**代码实现如下**：

```
assume cs:code

data segment
    db 'Welcome to masm!',0
data ends

code segment
    start:  mov dh,8
            mov dl,3
            mov cl,2
            mov ax,data
            mov ds,ax
            mov si,0
            call show_str
            
            mov ax,4c00h
            int 21h
            
;显示字符串子程序
 show_str:  push ax
            push bx
            push cx
            push dx
            push es
            push si
            push di
            ;将子程序使用到的的寄存器存放到栈中
 
            mov ax,0B800h
            mov es,ax                   ;设置显示缓存的段地址
            
            mov al,dh
            mov ah,0
            mov bx,160
            mul bx
            mov dh,0
            add ax,dx
            add ax,dx                   ;计算显示缓存中的偏移地址
            
            mov si,ax
            mov di,0
            mov bl,cl                   ;存储属性
show_str_s: mov cl,[di]                 ;设置字符
            sub ch,ch
            jcxz show_str_ok
            mov ch,bl                   ;设置属性
            mov es:[si],cx              ;显示数据
            
            add si,2
            inc di                      ;为显示下一个字符做准备
            jmp show_str_s
            
show_str_ok:pop di
            pop si
            pop es
            pop dx
            pop cx
            pop bx
            pop ax
            ;恢复寄存器的状态为调用子程序时的状态
            ret
            
 code ends
 
 end start
```



#### 2.解决除法溢出的问题

**问题**：通过 div 指令可以做除法。当进行 8 位除法的时候，用 al 存商，ah 存余数。当进行 16 位除法的时候，用 ax 存商，dx 存余数。但是会出现商大于 al 或 ax 所能存储的最大值，这个时候将引发 CPU 的一个内部错误：***除法溢出***。通过实现子程序 divdw 来防止出现这种问题。

**子程序描述**：

​	名称：divdw

​	功能：进行不会溢出的除法运算，被除数为 dword 型，除数为 word 型，结果为 dword 型。

​	参数：(ax) = dword 型数据的低 16 位

​				(dx) = dword 型数据的高 16 位

​				(cx) = 除数

​	返回：(dx) = 结果的高 16 位，(ax) = 结果的低 16 位

​				(cx) = 余数

​	例如：计算 1000000/10 (F4240H/0AH)

```
mov ax,4240h
mov dx,0000fh
mov cx,0ah
call divdw

;结果：(dx)=0001H，(ax)=86a0H,(cx)=0
```

**原理**：

设	X: 被除数，范围：[0, FFFFFFFFH]

​		N: 除数，范围：[0, FFFFH]

​		H: X 高 16 位，范围：[0, FFFFH]

​		L: X 低 16 位，范围：[0, FFFFH]

​		int(): 描述性运算法，取商。

​		rem(): 描述性运算符，取余数。

公式：X/N = int(H/N) * 65536 + [rem(H/N) * 65536] / N

**代码实现如下**：

```
assume cs:code

code segment

	start:	mov ax,4240H
			mov dx,000FH
			mov cx,0AH
			call divdw
			
			mov ax,4c00H
			int 21h
	
	divdw:	push bx
			push si
			
			mov si,dx
			mov dx,0		;记录dx 的值，并将 dx 清零
			 
			div cx			;H/N，商在 ax，余数在 dx
			mov bx,ax		; X/N 的高 16 位
			
			mov ax,si
			div cx			; [rem(H/N)*65536 + L] / N
			
			mov cx,dx		;余数
			mov dx,bx		; X/N 的高 16 位
			
			pop si
			pop bx
			ret
			
code ends

end start
```



#### 3. 数值显示

**问题**：编程，将 data 段中的数据以十进制的形式显示出来。



**子程序描述**：

​	名称：dtoc

​	功能：将 word 型数据转变为表示十进制数的字符串，字符串以 0 位结尾符。

​	参数：(ax) = word 型数据

​				ds:si 指向字符串的首地址

​	返回：无

​	应用举例：编程，将数据 12666 以十进制的形式在屏幕的 8 行 3 列，用绿色显示出来。在显示时我们调用本次实验中的第一个子程序 show_str。



**代码实现如下**：

```
assume cs:code

data segment
    db 10 dup (0)
data ends

code segment
    start:  mov ax,12666
            mov bx,data
            mov ds,bx
            mov si,0
            call dtoc
            
            mov dh,8
            mov dl,3
            mov cl,2
            call show_str
            
            mov ax,4c00h
            int 21h
            
     dtoc:  push ax
     		push bx
     		push cx
     		push dx
            push si
            push di
            
            mov si,0
            mov bx,10
            
   dtoc_s:  mov dx,0
   			div bx
            mov cx,ax
            add dl,30H
            mov [si],dl
            jcxz dtoc_reverse
            inc si
            jmp dtoc_s
        
dtoc_reverse: 
			mov di,0
dtoc_reverse_loop:
			mov cx,si
			sub cx,di
			jcxz dtoc_ok
			mov al,[di]
			mov bl,[si]
			mov [di],bl
			mov [si],al
			inc di
			dec si
			jmp dtoc_reverse_loop
   			;反转内容
   
  dtoc_ok:	pop di
   			pop si
   			pop dx
   			pop cx
            pop bx
            pop ax
            ret
            
    
;显示字符串子程序
 show_str:  push ax
            push bx
            push cx
            push dx
            push es
            push si
            push di
            ;将子程序使用到的的寄存器存放到栈中
 
            mov ax,0B800h
            mov es,ax                   ;设置显示缓存的段地址
            
            mov al,dh
            mov ah,0
            mov bx,160
            mul bx
            mov dh,0
            add ax,dx
            add ax,dx                   ;计算显示缓存中的偏移地址
            
            mov si,ax
            mov di,0
            mov bl,cl                   ;存储属性
show_str_s: mov cl,[di]                 ;设置字符
            sub ch,ch
            jcxz show_str_ok
            mov ch,bl                   ;设置属性
            mov es:[si],cx              ;显示数据
            
            add si,2
            inc di                      ;为显示下一个字符做准备
            jmp show_str_s
            
show_str_ok:pop di
            pop si
            pop es
            pop dx
            pop cx
            pop bx
            pop ax
            ;恢复寄存器的状态为调用子程序时的状态
            ret
code ends

end start
```



# 课程设计 1

任务：将实验 7 中的 Power idea 公司的数据按照如下格式在屏幕上显示出来。

![image-20200711161533715](picture/汇编语言(第3版)/image-20200711161533715.png)

**代码实现如下**：

```
assume cs:codesg,ds:data,ss:stack

data segment
  db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
  db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
  db '1993','1994','1995'
  ; 年份
  
  dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
  dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000
  ; 每年的收入
  
  dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
  dw 11542,14430,15257,17800
  ; 雇员人数
  
  dw 0,0,0
  ; 记录每个数组当前的下标，对应的起始偏移地址为 210、212、214
data ends

table segment
  db 21 dup ('year summ ne ?? ')
table ends

tmpdata segment
	db 80 dup (0)
tmpdata ends

stack segment
  dw 16 dup (0)
stack ends

codesg segment
start:	mov ax,data
		mov es,ax		;初始化数据源地址
		
		mov ax,stack
		mov ss,stack
		mov sp,10H		;初始化堆栈
		
		mov ax,tmpdata
		mov ds,ax		;初始化目的地址
		
		mov cx,21
		mov di,0
		mov bx,0
	s:	push cx
		
		mov cx,4
		mov si,0
	s0:	mov byte ptr [si],es:[bx].0[si]
		inc si
		pop cx
		loop s

	    mov ax,4c00H
      	int 21h
      
;显示字符串子程序
 show_str:  push ax
            push bx
            push cx
            push dx
            push es
            push si
            push di
            ;将子程序使用到的的寄存器存放到栈中
 
            mov ax,0B800h
            mov es,ax                   ;设置显示缓存的段地址
            
            mov al,dh
            mov ah,0
            mov bx,160
            mul bx
            mov dh,0
            add ax,dx
            add ax,dx                   ;计算显示缓存中的偏移地址
            
            mov si,ax
            mov di,0
            mov bl,cl                   ;存储属性
show_str_s: mov cl,[di]                 ;设置字符
            sub ch,ch
            jcxz show_str_ok
            mov ch,bl                   ;设置属性
            mov es:[si],cx              ;显示数据
            
            add si,2
            inc di                      ;为显示下一个字符做准备
            jmp show_str_s
            
show_str_ok:pop di
            pop si
            pop es
            pop dx
            pop cx
            pop bx
            pop ax
            ;恢复寄存器的状态为调用子程序时的状态
            ret
codesg ends

end start
```

